OCR_DOCKER_CONTAINER_NAME := "ocrmypdf"
OCR_DOCKERFILE := "./ocrmypdf.Dockerfile"
TESSERACT-OPTIONS := "-l ita+eng" # we keep english in the case of reports in english

# We can limit the resource usage of ocrmypdf
GPUS:=""
CPUS:=""
MEMORY:=""
# for usage in the cluster
# GPUS:="device=0"
# CPUS:="16"
# MEMORY:="32g"

LAYOUTPDFREADER_CONTAINER_NAME := "ghcr.io/nlmatics/nlm-ingestor:latest"
LAYOUTPDFREADER_DOCKER_IMAGE := "ghcr.io/nlmatics/nlm-ingestor:latest"

default:
  @just --list

[group("utils")]
get-filename FILEPATH:
  @echo -n "{{file_name(FILEPATH)}} "

[group("utils")]
get-files-in-dir DIR:
  for file in $(ls {{clean(DIR / "*.pdf")}}); do \
    just get-filename $file; \
  done


[group("build")]
ocr-build:
  podman build -t {{OCR_DOCKER_CONTAINER_NAME}} -f {{OCR_DOCKERFILE}} .

[group("build")]
layoutpdfreader-build:
  # docker pull {{LAYOUTPDFREADER_DOCKER_IMAGE}}


# OCR

# OCR all given INPUT_FILES inside INPUT_DIR and return them in OUTPUT_DIR
[group("OCR")]
ocr INPUT_DIR OUTPUT_DIR +INPUT_FILES:
  podman run --name {{OCR_DOCKER_CONTAINER_NAME}} -i --replace \
    --userns=keep-id \
    --workdir /data \
    -v "{{absolute_path(INPUT_DIR)}}:/data:ro" \
    -v "{{absolute_path(OUTPUT_DIR)}}:/data-output:rw" \
    {{(if MEMORY == "" {""} else { "--memory=" + MEMORY }) + \
    (if CPUS == "" {""} else {" --cpus=" + '"' + CPUS + '"'}) + \
    (if GPUS == "" {""} else {" --gpus=" + "'" + '"' + GPUS + '"' + "'"}) }} \
    {{OCR_DOCKER_CONTAINER_NAME}} \
    --tag -j {{(if CPUS == "" {num_cpus()} else {CPUS})}} "ocrmypdf {{TESSERACT-OPTIONS}} '/data/{}' '/data-output/{}' || cp '/data/{}' '/data-output/{}'" ::: {{INPUT_FILES}}

# TEXT EXTRACTOR

PDFTOTEXT_FLAGS := ""

[group("Text extractor")]
layoutpdfreader INPUT OUTPUT_DIR:
  # docker run -p 5010:5001 {{LAYOUTPDFREADER_DOCKER_IMAGE}}
  pdftotext {{PDFTOTEXT_FLAGS}} {{INPUT}} {{clean(OUTPUT_DIR / file_stem(INPUT) + ".txt")}}

[group("Text extractor")]
batch_layoutpdfreader INPUT_DIR OUTPUT_DIR:
  for file in `ls {{clean(INPUT_DIR / "*.pdf")}}`; do \
     just layoutpdfreader ${file} {{OUTPUT_DIR}}; \
  done

# FINAL RULES

[group("build")]
[group("main")]
build: ocr-build layoutpdfreader-build

[group("utils")]
clean_outputs DIR:
  @rm -f {{clean(DIR / "*.pdf")}}
  @rm -f {{clean(DIR / "*.txt")}}

DEFAULT_OCR_CACHE_DIR := "./cached_ocrs/"
DEFAULT_PDF_INPUT_DIR := "./inputs/"
DEFAULT_TEXT_OUTPUT_DIR := "./extracted_texts/"

[group("main")]
clean: (clean_outputs DEFAULT_OCR_CACHE_DIR) \
    (clean_outputs DEFAULT_PDF_INPUT_DIR) \
    (clean_outputs DEFAULT_TEXT_OUTPUT_DIR)

[group("main")]
extract_text INPUT_DIR OUTPUT_DIR OCR_CACHE_DIR +FILES:
    (clean_outputs OCR_CACHE_DIR) (ocr INPUT_DIR OCR_CACHE_DIR FILES) \
    (batch_layoutpdfreader OCR_CACHE_DIR OUTPUT_DIR)

